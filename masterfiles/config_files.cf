#body common control
#{
#  bundlesequence => {"configfiles"};
#  inputs => {"$(sys.workdir)/inputs/libraries/cfengine_stdlib.cf",};
#}

bundle agent configfiles
{
  vars:
    "files[sysctl]" string => "/tmp/sysctl.conf";
    "files[sshd]" string => "/tmp/sshd_config";
    "files[inittab]" string => "/tmp/inittab";

    "sysctl[net.ipv4.conf.all.accept_source_route]" string => "0"; 
    "sysctl[net.ipv4.tcp_syncookies]" string => "1"; 
    "sysctl[net.ipv4.conf.all.accept_redirects]" string => "0"; 
    "sysctl[net.ipv4.conf.all.rp_filter]" string => "1"; 
    "sysctl[net.ipv4.conf.all.log_martians]" string => "1"; 
    
    "sshd[Protocol]" string => "2";
    "sshd[UseDNS]" string => "no";
    "sshd[PermitRootLogin]" string => "no";
    "sshd[X11Forward]" string => "yes";

    "inittab[runlevel]" string =>"3";
 
  methods:
    "sysctl" usebundle => edit_sysctl,
            comment => "configure $(configfiles.files[sysctl])";
  
    "sshd" usebundle => edit_sshd,
      comment => "configure $(configfiles.files[sshd])";
   
    "inittab" usebundle => edit_inittab,
              comment => "configure $(configfiles.files[inittab])";
}

bundle agent edit_sysctl
{
  files:
    "$(configfiles.files[sysctl])"
      handle => "edit_sysctl",
      comment => "make sure sysctl.conf contains desired configuration setting",
      create => "true",
      classes => if_ok("sysctlfile");
    
    sysctlfile::
      "$(configfiles.files[sysctl])"
        edit_line => set_variable_values("configfiles.sysctl"),
        classes => if_repaired("sysctl_modified");

  commands:
    sysctl_modified.!no_restarts::
    "/usr/bin/cat /tmp/sysctl.conf"
      handle => "reload_sysctl",
      comment => "Make sure sysctl settings are applied immediately";
 
  reports:
    repair_failed|repair_timeout|repair_denied::
    "repair failed for some reason";
}    


bundle agent edit_sshd
{
  files:
    "$(configfiles.files[sshd])"
      handle => "sshd_config",
      comment => "edit sshd_config file",
      create => "true",
      edit_line => set_config_values("configfiles.sshd"),
      classes => if_repaired("restart_sshd");

  commands:
    "/usr/bin/systemctl restart sshd.service"
      comment => "restart sshd service",
      handle => "sshd_restart",
      ifvarclass => "restart_sshd";
}

bundle agent edit_inittab
{
  files:
    "$(configfiles.files[inittab])"
      handle => "set_inittab_default_runlevel",
      comment => "change default runlevel to $(configfiles.inittab[runlevel])",
      create => "false",
      edit_defaults => backup_timestamp,
      edit_line => set_colon_field("id","2","$(configfiles.inittab[runlevel])");

    "$(configfiles.files[inittab])"
      handle => "disable_inittab_ctrlaltdel",
      comment => "Disable inittab ctr alt del",
      create => "false",
      edit_defaults => backup_timestamp,
      edit_line => comment_lines_matching("ca::ctrlaltdel:.*","#");
}

